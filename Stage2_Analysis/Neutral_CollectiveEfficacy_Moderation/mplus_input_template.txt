TITLE: Aim1_ADI_con0009_moderation
DATA:
        File = "../voxeldata.csv";

VARIABLE:
        Names = twinid famid scan adi coleff soccoh infsocn norms
	age sex race income pceduc peqcttr V1-V468;
        
	Usevar = famid coleff age sex race scan V__VOXEL__ cexage;
	
	Missing = all (-99);      	
        
	Cluster = famid;

DEFINE:
	CENTER coleff age (GRANDMEAN);
	
	cexage = coleff*age; !x*w

ANALYSIS:
        Type = Complex;
        Estimator = ML;
	Bootstrap = 5000;

MODEL:
        V__VOXEL__ ON coleff (b1); !Y ON X
	V__VOXEL__ ON age (b2); !Y ON W
	V__VOXEL__ ON cexage (b3); !Y ON XW
	
	coleff ON age sex race scan;
	age ON sex race scan;
	V__VOXEL__ ON sex race scan;
	cexage ON sex race scan;

MODEL CONSTRAINT: 
	
	! Use model constraint subcommand to test simple slopes
	! Pick low, med, and high moderator values
	! e.g., 1 SD above mean, mean, and 1 SD below mean

	NEW(Low_age Med_age High_age Simp_lo Simp_med Simp_hi);

	Low_age = -2.24;
	Med_age = 0;
	High_age = 2.24;

	! Calc simple slopes for each value of age

	Simp_lo = b1 + b3*Low_age;
	Simp_med = b1 + b3*Med_age;
	Simp_hi = b1 + b3*High_age;

OUTPUT: 
        sampstat cinterval standardized;
